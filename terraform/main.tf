# -----------------------------------------------------------------------------
# Cloud-init: install base tools + Ansible on first boot (helps later steps)
# -----------------------------------------------------------------------------
locals {
  cloud_init = <<-EOF
  #cloud-config
  package_update: true
  packages:
    - ca-certificates
    - curl
    - jq
    - vim
    - python3
    - python3-pip
    - git
    - keepalived
    - nftables
    - conntrack
  runcmd:
    - pip3 install --upgrade pip
    - pip3 install ansible
    - systemctl enable keepalived || true
    - systemctl enable nftables || true
  ssh_authorized_keys:
  %{for k in var.ssh_authorized_keys~}
    - ${k}
  %{endfor~}
  EOF
}

# -----------------------------------------------------------------------------
# NAT-A
# -----------------------------------------------------------------------------
resource "linode_instance" "nat_a" {
  label     = "${var.prefix}-a"
  region    = var.region
  image     = var.image
  type      = var.type
  root_pass = var.root_pass

  # eth0: Public
  interface { purpose = "public" }

  # eth1: VLAN
  interface {
    purpose      = "vlan"
    label        = var.vlan_label
    ipam_address = var.nat_a_vlan_ip # e.g., 192.168.1.3/24
  }

  tags = ["nat", "gateway", "ha", "primary"]

  metadata {
    user_data = base64encode(local.cloud_init)
  }
}

# -----------------------------------------------------------------------------
# NAT-B
# -----------------------------------------------------------------------------
resource "linode_instance" "nat_b" {
  label     = "${var.prefix}-b"
  region    = var.region
  image     = var.image
  type      = var.type
  root_pass = var.root_pass

  # eth0: Public
  interface { purpose = "public" }

  # eth1: VLAN
  interface {
    purpose      = "vlan"
    label        = var.vlan_label
    ipam_address = var.nat_b_vlan_ip # e.g., 192.168.1.4/24
  }

  tags = ["nat", "gateway", "ha", "backup"]

  metadata {
    user_data = base64encode(local.cloud_init)
  }
}

# -----------------------------------------------------------------------------
# Authorize the additional public IPv4 (shared_ipv4) for these Linodes
# (keeps the IP owned by customer's anchor; allows nat-a/b to bind it)
# -----------------------------------------------------------------------------
# Share the additional IPv4 to nat-a
resource "null_resource" "ip_share_nat_a" {
  count = var.use_ip_share && length(var.shared_ipv4) > 0 ? 1 : 0

  provisioner "local-exec" {
    command = <<-EOC
      set -euo pipefail
      echo "Sharing ${var.shared_ipv4} to nat-a (linode_id=${linode_instance.nat_a.id})"
      curl -sS -H "Authorization: Bearer ${var.linode_token}" \
           -H "Content-Type: application/json" \
           -X POST \
           -d '{"ips":["${var.shared_ipv4}"],"linode_id": ${linode_instance.nat_a.id}}' \
           https://api.linode.com/v4/networking/ips/share >/dev/null
      echo "Share to nat-a completed."
    EOC
  }

  depends_on = [linode_instance.nat_a]
}

# Share the additional IPv4 to nat-b
resource "null_resource" "ip_share_nat_b" {
  count = var.use_ip_share && length(var.shared_ipv4) > 0 ? 1 : 0

  provisioner "local-exec" {
    command = <<-EOC
      set -euo pipefail
      echo "Sharing ${var.shared_ipv4} to nat-b (linode_id=${linode_instance.nat_b.id})"
      curl -sS -H "Authorization: Bearer ${var.linode_token}" \
           -H "Content-Type: application/json" \
           -X POST \
           -d '{"ips":["${var.shared_ipv4}"],"linode_id": ${linode_instance.nat_b.id}}' \
           https://api.linode.com/v4/networking/ips/share >/dev/null
      echo "Share to nat-b completed."
    EOC
  }

  depends_on = [linode_instance.nat_b]
}

#Terraform writes ansible/inventory.ini
resource "local_file" "ansible_inventory" {
  filename = "${path.module}/../ansible/inventory.ini"
  content  = <<-EOT
[nat]
nat-a ansible_host=${tolist(linode_instance.nat_a.ipv4)[0]} state=MASTER priority=150
nat-b ansible_host=${tolist(linode_instance.nat_b.ipv4)[0]} state=BACKUP priority=100

[nat:vars]
ansible_user=root
pub_if=eth0
vlan_if=eth1
EOT

  depends_on = [
    linode_instance.nat_a,
    linode_instance.nat_b
  ]
}

#auto-generating a ansible vars file 
resource "local_file" "ansible_group_vars_nat" {
  filename = "${path.module}/../ansible/group_vars/nat.yml"
  content  = <<-EOT
# Auto-generated by Terraform. Do not edit by hand.
fip: "${var.shared_ipv4}"
vlan_vip: "${var.vlan_vip}"
dcid: ${var.dcid}

# Interfaces (match what Terraform created on the Linodes)
pub_if: "eth0"
vlan_if: "eth1"
EOT

  depends_on = [
    linode_instance.nat_a,
    linode_instance.nat_b
  ]
}

resource "null_resource" "create_hostvars_dir" {
  provisioner "local-exec" {
    command = "mkdir -p ../ansible/host_vars"
  }
}

resource "local_file" "hostvars_nat_a" {
  filename = "${path.module}/../ansible/host_vars/nat-a.yml"
  content  = <<-EOT
    # Auto-generated by Terraform — do not edit manually
    vlan_if: eth1
    ct_local_ip: "${trimsuffix(var.nat_a_vlan_ip, "/24")}"
    ct_peer_ip:  "${trimsuffix(var.nat_b_vlan_ip, "/24")}"
  EOT

  depends_on = [
    linode_instance.nat_a,
    linode_instance.nat_b,
    null_resource.create_hostvars_dir
  ]
}

resource "local_file" "hostvars_nat_b" {
  filename = "${path.module}/../ansible/host_vars/nat-b.yml"
  content  = <<-EOT
    # Auto-generated by Terraform — do not edit manually
    vlan_if: eth1
    ct_local_ip: "${trimsuffix(var.nat_b_vlan_ip, "/24")}"
    ct_peer_ip:  "${trimsuffix(var.nat_a_vlan_ip, "/24")}"
  EOT

  depends_on = [
    linode_instance.nat_a,
    linode_instance.nat_b,
    null_resource.create_hostvars_dir
  ]
}
